plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "io.github.patrick.remapper" version "1.4.2"
}

group = 'at.justacasualday'
version = '1.0'

repositories {
    mavenLocal();
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"

    }
    maven {
        name = "private-server-repo"
        url = uri(findProperty("PRIVATE_MAVEN_SERVER") ?: System.getenv("PRIVATE_MAVEN_SERVER"))
        allowInsecureProtocol = true
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot:1.21.8-R0.1-SNAPSHOT:remapped-mojang")
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21")
    }

    remap {
        version.set("1.21.8")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain {
            languageVersion = JavaLanguageVersion.of(targetJavaVersion)
            vendor = JvmVendorSpec.ADOPTOPENJDK
            build {
                repositories { mavenCentral() }
            }
        }
    }
}

tasks.build {
    dependsOn remap
}

tasks.jar {
    archiveBaseName.set('JustACasualAPI')
    archiveVersion.set('1.0') // or match your tag: '1.0.11'
    dependsOn remap
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.withType(Javadoc) {
    failOnError = false // This disables build failure on Javadoc warnings
    options.addStringOption('Xdoclint:none', '-quiet') // Disables strict doc checks
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
